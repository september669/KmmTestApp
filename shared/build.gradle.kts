import org.jetbrains.kotlin.gradle.plugin.mpp.KotlinNativeTarget

plugins {
    kotlin("multiplatform")
    id("com.android.library")
}

kotlin {

    android()

    ios {
        binaries {
            framework {
                baseName = "shared"
            }
        }
    }


    sourceSets {

        val coroutinesVersion = "1.4.2-native-mt"

        val commonMain by getting {
            dependencies {

                //  coroutines
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion")

                //  logging
                api("org.dda.ankoLogger:AnkoLogger:0.2.1")

                //  http client
                implementation("io.ktor:ktor-client-core:1.5.0")

                //  DI
                implementation("org.kodein.di:kodein-di:7.2.0")

                //  atomic operations
                implementation("org.jetbrains.kotlinx:atomicfu:0.15.0")

            }
        }
        val commonTest by getting {
            dependencies {
                implementation(kotlin("test-common"))
                implementation(kotlin("test-annotations-common"))
            }
        }
        val androidMain by getting {
            dependencies {
                //implementation("com.google.android.material:material:1.2.1")
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion")

                val moxyVersion = "2.2.1"
                implementation("com.github.moxy-community:moxy:$moxyVersion")
            }
        }
        val androidTest by getting {
            dependencies {
                implementation(kotlin("test-junit"))
                implementation("junit:junit:4.13.1")
            }
        }
        val iosMain by getting {
            dependencies {
                //implementation("com.google.android.material:material:1.2.1")
                //implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion")
            }
        }
        val iosTest by getting
    }
}

android {


    compileSdkVersion(30)
    sourceSets["main"].manifest.srcFile("src/androidMain/AndroidManifest.xml")
    defaultConfig {
        minSdkVersion(20)
        targetSdkVersion(30)

        multiDexEnabled = true

        buildConfigField("String", "LOGGING_TAG", "\"TEST_APP\"")

    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

val packForXcode by tasks.creating(Sync::class) {
    group = "build"
    val mode = System.getenv("CONFIGURATION") ?: "DEBUG"
    val sdkName = System.getenv("SDK_NAME") ?: "iphonesimulator"
    val targetName = "ios" + if (sdkName.startsWith("iphoneos")) "Arm64" else "X64"
    val framework =
        kotlin.targets.getByName<KotlinNativeTarget>(targetName).binaries.getFramework(mode)
    inputs.property("mode", mode)
    dependsOn(framework.linkTask)
    val targetDir = File(buildDir, "xcode-frameworks")
    from({ framework.outputDirectory })
    into(targetDir)
}

tasks.getByName("build").dependsOn(packForXcode)